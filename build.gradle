buildscript {
    ext {
        spockVersion = '1.3-groovy-2.5'
        springCloudVersion = 'Greenwich.SR1'
    }
}

plugins {
    id 'org.springframework.boot' version '2.1.4.RELEASE'
    id 'java'
    id 'groovy'
}

apply plugin: 'io.spring.dependency-management'

group = 'pl.arczynskiadam.cojesc'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_12

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'io.github.openfeign:feign-core:10.2.3' // Spring Cloud version override due to bug: https://github.com/OpenFeign/feign/issues/924
    implementation platform(group: "org.springframework.cloud", name: 'spring-cloud-dependencies', version: "$springCloudVersion")

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.spockframework:spock-spring:$spockVersion"
    testImplementation "org.spockframework:spock-core:$spockVersion"
    testImplementation 'com.github.tomakehurst:wiremock-standalone:2.23.2'
}

task ensureCredentialsExist {
    doFirst {
        if (!new File('src/main/resources/facebook-credentials.yaml').exists()) {
            throw new IllegalStateException(
                    """\
                    |Facebook API credentials not found. 
					|Please create facebook-credentials.yaml file in resurces folder
					|It must contain following keys:
					|- cojesc.http.client.facebook.appId
					|- cojesc.http.client.facebook.secret
					""".stripMargin()
            )
        }
    }
}

assemble.dependsOn(ensureCredentialsExist)
